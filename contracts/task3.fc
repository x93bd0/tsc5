#include "imports/stdlib.fc";

int cell_null?(cell c) asm "ISNULL";
(slice, int) udict_get?(cell dict, int key_len, int index) asm(index dict key_len) "DICTUGET" "NULLSWAPIFNOT";
int slice_empty?(slice s) asm "SEMPTY";


cell get_storage() {
  slice s = get_data().begin_parse();
  return s~load_ref();
}

() wrap_storage(cell storage, int version_id) impure {
  set_data(begin_cell()
    .store_uint(version_id, 32)
    .store_ref(storage)
  .end_cell());
}

int version() method_id {
  slice cs = get_data().begin_parse();
  return cs~load_uint(32);
}

() recv_internal(int msg_value, int balance, cell in_msg_full, slice in_msg_body) impure {
  int ev = in_msg_body~load_uint(32);
  if (ev == 0)
  {
    set_data(begin_cell()
      .store_uint(1, 32)
      .store_ref(get_data())
    .end_cell());
    return ();
  }

  (cell ec, cell mi, cell pl) = (
    in_msg_body~load_maybe_ref(), in_msg_body~load_dict(), in_msg_body~load_ref());

  slice dat = get_data().begin_parse();
  (int v, cell st) = (dat~load_uint(32), dat~load_ref());

  if (ev > v)
  {
    throw_if(200, cell_null?(ec));
    while (ev > v)
    {
      (slice val, int fl) = udict_get?(mi, 32, v);
      if (fl == 0) { throw(400); }
      v = val~load_uint(32);
      cell ref = val~load_maybe_ref();

      ifnot (cell_null?(ref))
      {
        set_c3(ref.begin_parse().bless());
        st = migrate_one(st);
      }
    } throw_if(400, ev != v);

    set_code(ec);
    set_c3(ec.begin_parse().bless());
  }

  throw_if(400, ev != v);
  st = process_message(
    st, msg_value, balance, in_msg_full, pl.begin_parse());
  set_data(begin_cell()
    .store_uint(v, 32)
    .store_ref(st)
  .end_cell());
}

;; <<<<< Custom version-specific code begins
;; This section (everything between << and >> characters) will be fully substituted for each version.
;; This is an IMPORTANT part, and these exact lines with <<<<< and >>>>> must be present in your code for the testing system to work correctly.
;; All the code provided here serves as an example of the version-code, which your update code must be compatible with.
;; Refer to the "3-example" directory for more version examples.

;; from counter-v0.fc
cell process_message(cell storage, int msg_value, int balance, cell in_msg_full, slice in_msg_body) impure {
    slice cs = storage.begin_parse();
    int current_amount = cs.preload_uint(32);
    return begin_cell().store_uint(current_amount + 1, 32).end_cell();
}

cell migrate_one(cell old_storage) { ;; it's just a placeholder that is required for correct compilation
    return old_storage;
}

;; Custom version-specific code ends >>>>>
